apply plugin: 'com.palantir.external-publish-jar'
apply plugin: 'org.revapi.revapi-gradle-plugin'
apply plugin: 'me.champeau.jmh'

dependencies {
    implementation 'com.fasterxml.jackson.core:jackson-annotations'
    implementation 'com.google.guava:guava'
    implementation 'com.palantir.safe-logging:preconditions'
    implementation 'com.palantir.safe-logging:safe-logging'

    testImplementation 'com.fasterxml.jackson.core:jackson-databind'
    testImplementation 'com.palantir.safe-logging:preconditions-assertj'
    testImplementation 'org.assertj:assertj-core'
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.junit.jupiter:junit-jupiter-api'

    jmhAnnotationProcessor 'org.openjdk.jmh:jmh-generator-annprocess'
    jmh 'com.fasterxml.jackson.core:jackson-annotations'
}

jmh {
    // Use profilers to collect additional data. Supported profilers:
    // [cl, comp, gc, jfr, stack, perf, perfnorm, perfasm, xperf, xperfasm, hs_cl, hs_comp, hs_gc, hs_rt, hs_thr]
    //profilers = ['jfr']
    //jvmArgsPrepend = ['-XX:+UnlockDiagnosticVMOptions', '-XX:+DebugNonSafepoints']
    humanOutputFile = project.file("${resultsDir(project)}/results.txt")
    resultsFile = project.file("${resultsDir(project)}/results.json")
    resultFormat = 'JSON'
}

// https://github.com/melix/jmh-gradle-plugin/issues/248
tasks.jmhCompileGeneratedClasses {
    options.annotationProcessorPath = configurations.jmhAnnotationProcessor
}

plugins.withId("net.ltgt.errorprone") {
    tasks.jmhCompileGeneratedClasses {
        options.errorprone {
            enabled = true
            // JMH generates duplicate field names like byte p000, p001, p002
            disable("HidingField")
        }
    }
}

def resultsDir(Project project) {
    String circleArtifactsDir = System.getenv("CIRCLE_ARTIFACTS");
    if (circleArtifactsDir == null) {
        return project.getBuildDir().toString() + "/results/jmh";
    }
    return circleArtifactsDir + "/jmh/" + project.getName();
}

tasks.check.dependsOn('jmh')
